|| day03.am -- Lobby
||
|| The first version of this used memoization of a search with and without using each digit.
|| This version takes advantage of a look-ahead window to drop any digit if there's a better
|| one available.


%export day03

%import "adventLib"


computeJoltage :: int -> [char] -> int
computeJoltage nd ds
    = go len nd ds |> intval
      where
        len = #ds

        go r 0 ds = []
        go r n [] = []
        go r n (d : ds)
            = go r' n ds,           if canSkip & hasBetter
            = d : go r' (n - 1) ds, otherwise
              where
                r'        = r - 1
                canSkip   = r > n
                hasBetter = any (>. d) $ take (r - n) ds
 
day03 :: io ()
day03
    = readInput "day03" >>= lines .> go
      where
        go banks
            = output [part1, part2]
              where
                part1 = process 2
                part2 = process 12

                process n = map (computeJoltage n) banks |> sum |> showint

         
