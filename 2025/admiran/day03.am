|| day03.am -- Lobby


%export day03

%import "adventLib"
%import <map>
%import <maybe>
%import <memo>
%import <state>


|| key used in memoized map lookup
key == (int, string)

|| compare two joltage strings by comparing their integer values
cmpjolt :: string -> string -> ordering
cmpjolt a b = cmpint (intval a) (intval b)

computeJoltage :: int -> [char] -> int
computeJoltage n ds
    = st_evalState (findBest (n, ds)) (cmpkey, m_empty) |> intval
      where
        findBest = memo findBest'

        findBest' (0, ds) = st_pure []
        findBest' (n, []) = st_pure []
        findBest' (n, d : ds)
            = st_liftA2 (max2 cmpjolt) bestWith bestWithout
              where
                bestWith    = (d :) <$> findBest (n - 1, ds)
                bestWithout = findBest (n, ds)
 
day03 :: io ()
day03
    = readInput "day03" >>= lines .> go
      where
        go banks
            = output [part1, part2]
              where
                part1 = process 2
                part2 = process 12

                process n = map (computeJoltage n) banks |> sum |> showint

         
